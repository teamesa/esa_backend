plugins {
	id 'org.springframework.boot' version '2.5.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

allprojects {
	group 'kilometer'
	version '1.0'

	repositories {
		mavenCentral()
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'groovy'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	sourceCompatibility = 11

	dependencies {
		// lombok을 위한 의존성
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testCompileOnly 'org.projectlombok:lombok'
		testAnnotationProcessor 'org.projectlombok:lombok'

		// test를 위한 의존성
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'org.spockframework:spock-core:2.2-M1-groovy-4.0'
		testImplementation 'org.spockframework:spock-spring:2.2-M1-groovy-4.0'

		// loggin을 위한 의존성
		implementation 'org.springframework.boot:spring-boot-starter-log4j2'
		modules {
			module("org.springframework.boot:spring-boot-starter-logging") {
				replacedBy("org.springframework.boot:spring-boot-starter-log4j2", "Use Log4j2 instead of Logback")
			}
		}
	}

	test {
		useJUnitPlatform()
	}

	bootJar {
		enabled = false
	}

	jar {
		enabled = true
	}
}
